/**
 * Build configuration for the Flutter Zoom Meeting SDK Android plugin.
 * 
 * This file configures the Android library module that provides Zoom meeting
 * functionality to Flutter applications. It includes the Zoom MobileRTC SDK
 * and all required dependencies.
 */

group = "dev.mateenmehmood.flutter_zoom_meeting_sdk"
version = "1.0-SNAPSHOT"

// Buildscript dependencies for Gradle plugins
buildscript {
    ext.kotlin_version = "1.9.23"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.11.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

// Apply Android library and Kotlin plugins
apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply from: "dependencies.gradle"

// Android build configuration
android {
    namespace = "dev.mateenmehmood.flutter_zoom_meeting_sdk"

    compileSdk = 36

    // Java 17 compatibility
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    // Include Kotlin source directories
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 28
    }

    // Test configuration
    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }

    packagingOptions {
        jniLibs {
            // Resolve duplicate libc++_shared.so coming from multiple native deps.
            pickFirsts += ["lib/**/libc++_shared.so"]
        }
    }

    buildFeatures {
        viewBinding = true
    }
}

// Project dependencies
dependencies {
    // Zoom MobileRTC SDK
    api("us.zoom:mobilertc:6.0.0")
    
    // Zoom SDK required dependencies (from versions.gradle)
    implementation(libraries.securityCrypto)
    implementation(libraries.cryptoTink)
    implementation(libraries.exoplayerCore)
    implementation(libraries.exoplayerUI)
    implementation(libraries.swiperefreshlayout)
    implementation(libraries.supportAppcompat)
    implementation(libraries.constraintlayout)
    implementation(libraries.supportDesign)
    implementation(libraries.multidex)
    implementation(libraries.gson)
    implementation(libraries.glideAnnotations)
    implementation(libraries.glideGlide)
    implementation(libraries.recyclerview)
    implementation(libraries.lottie)
    implementation(libraries.supportWindow)
    implementation(libraries.windowJava)
    implementation(libraries.jetbrainskotlin)
    implementation(libraries.kotlinStdlibJdk)
    implementation(libraries.kotlinxCoroutinesAndroid)
    implementation(libraries.kotlinxCoroutinesCore)
    implementation(libraries.androidxCoreKtx)
    implementation(libraries.androidxCore)
    implementation(libraries.viewmodelKtx)
    implementation(libraries.lifecycleKtx)
    implementation(libraries.lifecycleService)
    implementation(libraries.workerManager)
    implementation(libraries.fragmentKtx)
    implementation(libraries.scaleImageView)
    implementation(libraries.splashScreenLib)
    implementation(libraries.livedataKtx)
    implementation(libraries.rxAndroid)
    implementation(libraries.composeMaterial3)
    implementation(libraries.composeMaterial3WindowSize)
    implementation(libraries.composeUIToolingPreview)
    implementation(libraries.composeConstraintlayout)
    implementation(libraries.composeActivity)
    implementation(libraries.composeLifecycle)
    implementation(libraries.composeNavigation)
    implementation(libraries.coil)
    implementation(libraries.androidxWebkitKtx)
    implementation(libraries.jetbrainsAnnotations)
    implementation(libraries.kotlinxCoroutinesCoreJvm)
    implementation(libraries.kotlinxCoroutinesSwing)
    implementation(libraries.cameraXCore)
    implementation(libraries.cameraXCamera2)
    implementation(libraries.cameraXLifecycle)
    implementation(libraries.cameraXView)
    implementation(libraries.mlkitTextRecognition)
    implementation(libraries.blueparrottsdk)
    implementation(libraries.composeLifecycleViewmodel)
    implementation(libraries.profileinstaller)
    implementation(libraries.coilGif)
    implementation(libraries.viewBindingX)

    // Test dependencies
    testImplementation("org.jetbrains.kotlin:kotlin-test")
    testImplementation("org.mockito:mockito-core:5.0.0")
}
